PREFIX om-owl: <http://knoesis.wright.edu/ssw/ont/sensor-observation.owl#>
PREFIX weather: <http://knoesis.wright.edu/ssw/ont/weather.owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl-time: <http://www.w3.org/2006/time#>

SELECT ?sensor
WHERE {
  { ?observation om-owl:procedure ?sensor ;
                 a weather:VisibilityObservation ;
                 om-owl:result [om-owl:floatValue ?value ] ;
                 om-owl:samplingTime ?instant .
    ?instant owl-time:inXSDDateTime ?time .
    FILTER ( ?value < "10"^^xsd:float)  # centimeters
    FILTER (?time>="2003-04-03T16:00:00"^^xsd:dateTime && ?time<"2003-04-03T17:00:00"^^xsd:dateTime)
  }
  UNION
  { ?observation om-owl:procedure ?sensor ;
                 a weather:RainfallObservation ;
                 om-owl:result [om-owl:floatValue ?value ] ;
                 om-owl:samplingTime ?instant .
    ?instant owl-time:inXSDDateTime ?time .
    FILTER ( ?value > "30"^^xsd:float)  # centimeters
    FILTER (?time>="2003-04-03T16:00:00"^^xsd:dateTime && ?time<"2003-04-03T17:00:00"^^xsd:dateTime)
  }
  UNION
  { ?observation om-owl:procedure ?sensor ;
                 a weather:SnowfallObservation ;
                 om-owl:samplingTime ?instant .
    ?instant owl-time:inXSDDateTime ?time .
    FILTER (?time>="2003-04-03T16:00:00"^^xsd:dateTime && ?time<"2003-04-03T17:00:00"^^xsd:dateTime)
  }
}