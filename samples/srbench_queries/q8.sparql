PREFIX om-owl: <http://knoesis.wright.edu/ssw/ont/sensor-observation.owl#>
PREFIX weather: <http://knoesis.wright.edu/ssw/ont/weather.owl#>
PREFIX wgs84_pos: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl-time: <http://www.w3.org/2006/time#>

SELECT ( MIN(?temperature) AS ?minTemperature ) ( MAX(?temperature) AS ?maxTemperature )
WHERE {
  	?sensor om-owl:processLocation ?sensorLocation ;
          om-owl:generatedObservation ?observation .
  	?sensorLocation wgs84_pos:alt "5350"^^xsd:float ;
                  wgs84_pos:lat "40.82944"^^xsd:float ;
                  wgs84_pos:long "-111.88222"^^xsd:float .
  	?observation om-owl:observedProperty weather:_AirTemperature ;
               om-owl:result [ om-owl:floatValue ?temperature ] ;
               om-owl:samplingTime ?instant .
		?instant owl-time:inXSDDateTime ?time .
	FILTER (?time>"2003-04-01T00:00:00"^^xsd:dateTime && ?time<"2003-04-02T00:00:00"^^xsd:dateTime)
}
GROUP BY ?sensor